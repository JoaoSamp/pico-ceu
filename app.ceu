emit WINDOW_SET_SIZE(500,500,50,50);
emit GFX_SET_GRID(true);
await 1s;
emit GFX_CLEAR();
emit GFX_DRAW_RECT(25,0,25,25);
emit GFX_DRAW_BMP("pico-ceu/star.bmp", 30,5);

par do
    var int i;
    loop i in [1->_[, 2 do
        await 1s;
        emit GFX_SET_RGB(0xFF,0,0);
        emit GFX_DRAW_PIXEL(i,i);
        await 1s;
        emit GFX_SET_RGB(0,0xFF,0);
        emit GFX_DRAW_PIXEL(i+1,i+1);
    end
with
    await 5s;
    emit GFX_SET_RGB(0xFF,0xFF,0xFF);
    emit GFX_CLEAR();
with
    loop do
        var bool press;
        var u16 key;
        (press,key) = await KEY_PRESS until press;
        {printf(">>> press %c\n", @key);}
        (press,key) = await KEY_PRESS until not press;
        {printf(">>> unpress %c\n", @key);}
    end
with
    loop do
        var bool click;
        var u16 x;
        var u16 y;
        (click,_,x,y) = await MOUSE_CLICK until click;
        {printf(">>> click %d %d\n", @x,@y);}
        (click,_,x,y) = await MOUSE_CLICK until not click;
        {printf(">>> unclick %d %d\n", @x,@y);}
    end
with
    var u16 x;
    var u16 y;
    every (x,y) in MOUSE_MOVE do
        {printf(">>> move: %d %d\n", @x,@y);}
    end
with
    emit GFX_SET_TEXT_CURSOR(1,5);
    spawn do
        await 5s;
        emit GFX_SET_TEXT_FONT("pico-ceu/FreeSans.ttf", 10);
        emit GFX_SET_TEXT_CURSOR(1,5);
        await 2s;
        emit GFX_SCREENSHOT("pico-ceu/screen.bmp");
    end
    every 1s do
        emit GFX_DRAW_TEXT("|");
        emit GFX_DRAW_TEXTLN(" Ola Mundo");
    end
end
